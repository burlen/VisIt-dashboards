#-----------------------------------------------------------------------------
# general config
set(DASHBOARD_TYPE $ENV{DASHBOARD_TYPE})
set(CTEST_PROJECT_NAME "VisIt")
if (NOT DEFINED ALWAYS_UPDATE)
    if (DASHBOARD_TYPE STREQUAL "Continuous")
        set(ALWAYS_UPDATE OFF)
    else()
        set(ALWAYS_UPDATE ON)
    endif()
endif()
if (NOT DEFINED INCREMENTAL_BUILD)
    if (DASHBOARD_TYPE STREQUAL "Continuous")
        set(INCREMENTAL_BUILD ON)
    else()
        set(INCREMENTAL_BUILD OFF)
    endif()
endif()
set(CTEST_SOURCE_DIRECTORY "${CTEST_DASHBOARD_ROOT}/visit-src-${DASHBOARD_TYPE}")
set(CTEST_BINARY_DIRECTORY "${CTEST_DASHBOARD_ROOT}/visit-build-${DASHBOARD_TYPE}")
if (NOT CTEST_TEST_TIMEOUT)
    set(CTEST_TEST_TIMEOUT 120)
endif()
set(CTEST_UPDATE_COMMAND "${CTEST_SVN_COMMAND}")
#-----------------------------------------------------------------------------
# run the tests
if (NOT INCREMENTAL_BUILD)
    ctest_empty_binary_directory(${CTEST_BINARY_DIRECTORY})
    file(WRITE "${CTEST_BINARY_DIRECTORY}/CMakeCache.txt"
        ${INITIAL_CACHE})
endif()
ctest_start("${DASHBOARD_TYPE}")
set(numUpdated 0)
ctest_update(SOURCE "${CTEST_SOURCE_DIRECTORY}" RETURN_VALUE numUpdated)
if (ALWAYS_UPDATE OR (numUpdated GREATER 0))
    ctest_configure()
    ctest_read_custom_files("${CTEST_BINARY_DIRECTORY}")
    ctest_build()
    ctest_test()
    ctest_submit()
endif()
